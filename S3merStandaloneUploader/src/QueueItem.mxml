<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="400" height="21" 
	implements="mx.controls.listClasses.IListItemRenderer">
	<mx:Script>
		<![CDATA[
			import mx.controls.ProgressBarMode;
			import mx.controls.Label;
			import mx.controls.listClasses.IListItemRenderer;
			
			private var _data:Object;
			
			private function getPb():String {
				var fileObj:FileObject = _data as FileObject;

				if(!fileObj) {
					return null;
				}

				if(fileObj.complete == true) {
					pbPercent.mode = ProgressBarMode.MANUAL;
					pbPercent.setProgress(100,100);
					return "Complete";
				} else if(fileObj.error == true ) {
					pbPercent.mode = ProgressBarMode.MANUAL;
					pbPercent.setProgress(0,100);
					return "Error";
				} else if(fileObj.uploading == true ) {
					pbPercent.mode = ProgressBarMode.EVENT;
					return "Uploading %3%%";
				} else if(fileObj.nospace == true ) {
					pbPercent.mode = ProgressBarMode.EVENT;
					return "No Space Available";
				} else {
					pbPercent.mode = ProgressBarMode.MANUAL;
					pbPercent.setProgress(0,100);
					return "Waiting...";
				}
			}
			
			private function doDelete():void {
				dispatchEvent(new QueueEvent(QueueEvent.DELETE,data as FileObject, true));
			}
			
	        override public function get data():Object {
	            if(_data != null) {
	                return _data;
	            }
	            return null;
	        }
	
	        override public function set data(value:Object):void {
	        	var file:File;
	        	var name:String;
	        	
	            _data = value;
	            if(_data != null) {
	            	file = (data as FileObject).file;
	            	name = file.name;
	            	
	                lblFilename.text = name;
	                lblFilename.toolTip = name;
	                this.toolTip = name;
	                
	                pbPercent.label= getPb();
	                pbPercent.source = file;
	            }
	        }

			
		]]>
	</mx:Script>
			<mx:Label verticalCenter="0" left="10" id="lblFilename" width="249"/>
			<mx:ProgressBar id="pbPercent" mode="event" labelPlacement="center" verticalCenter="0" right="33" left="267" minimum="0" maximum="100"/>
			<mx:SWFLoader source="assets/deleteIcon.swf" height="15" width="15" verticalCenter="0" right="10" click="doDelete()"/>
</mx:Canvas>
